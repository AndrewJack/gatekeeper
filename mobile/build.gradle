apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def enableFabric = file('fabric.properties').exists()
if (enableFabric) {
    apply plugin: 'io.fabric'
}

kotlin { experimental { coroutines 'enable' } }

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "technology.mainthread.apps.gatekeeper"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true

        resValue 'string', 'particle_auth', appProps["particle_auth"] != null ? appProps['particle_auth'] : "Authorization: particle-auth"
        resValue 'string', 'particle_device', appProps["particle_device"] != null ? appProps["particle_device"] : ''
        resValue 'string', 'bucket_name', appProps["bucket_name"] != null ? appProps["bucket_name"] : ''

        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        if (keyPropsDebug["store"] != null) {
            debug {
                storeFile keyPropsDebug["store"] != null ? file(keyPropsDebug["store"]) : null
                keyAlias keyPropsDebug["alias"] ?: ""
                storePassword keyPropsDebug["storePass"] ?: ""
                keyPassword keyPropsDebug["pass"] ?: ""
            }
        }

        release {
            storeFile keyPropsRelease["store"] != null ? file(keyPropsRelease["store"]) : null
            keyAlias keyPropsRelease["alias"] ?: ""
            storePassword keyPropsRelease["storePass"] ?: ""
            keyPassword keyPropsRelease["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
            versionNameSuffix " debug"
            resValue "string", "app_name", "Gatekeeper debug"
        }
        release {
            if (keyPropsRelease["store"] != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Gatekeeper"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    kapt "com.android.databinding:compiler:$android_plugin_version"

    // Dagger 2
    compileOnly "javax.annotation:jsr250-api:1.0"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Play Services
    compile "com.google.android.gms:play-services-wearable:$commonPlayServicesVersion"

    // Firebase
    implementation "com.google.firebase:firebase-core:$commonPlayServicesVersion"
    implementation "com.google.firebase:firebase-config:$commonPlayServicesVersion"
    implementation "com.google.firebase:firebase-database:$commonPlayServicesVersion"
    implementation "com.google.firebase:firebase-storage:$commonPlayServicesVersion"
    implementation "com.google.firebase:firebase-auth:$commonPlayServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$commonPlayServicesVersion"
    implementation "com.google.firebase:firebase-messaging:$commonPlayServicesVersion"
    implementation 'com.firebaseui:firebase-ui-database:1.1.1'

    // Android support libraries
    implementation "com.android.support:support-v4:${commonSupportLibraryVersion}"
    implementation "com.android.support:support-v13:${commonSupportLibraryVersion}"
    implementation "com.android.support:appcompat-v7:${commonSupportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${commonSupportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${commonSupportLibraryVersion}"
    implementation "com.android.support:design:${commonSupportLibraryVersion}"
    implementation "com.android.support:preference-v14:${commonSupportLibraryVersion}"

    // rx
    implementation "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.0'

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    // Other
    implementation 'com.squareup.okio:okio:1.13.0'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation('de.psdev.licensesdialog:licensesdialog:1.8.3') { exclude module: 'support-v4' }

}

if (file("google-services.json").exists()) {
    apply plugin: 'com.google.gms.google-services'
} else {
    android.defaultConfig {
        resValue 'string', 'default_web_client_id', 'temp_client_id'
    }
}
