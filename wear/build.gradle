apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "technology.mainthread.apps.gatekeeper"
        minSdkVersion 23
        targetSdkVersion targetSdk
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        if (keyPropsDebug["store"] != null) {
            debug {
                storeFile keyPropsDebug["store"] != null ? file(keyPropsDebug["store"]) : null
                keyAlias keyPropsDebug["alias"] ?: ""
                storePassword keyPropsDebug["storePass"] ?: ""
                keyPassword keyPropsDebug["pass"] ?: ""
            }
        }

        release {
            storeFile keyPropsRelease["store"] != null ? file(keyPropsRelease["store"]) : null
            keyAlias keyPropsRelease["alias"] ?: ""
            storePassword keyPropsRelease["storePass"] ?: ""
            keyPassword keyPropsRelease["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
            versionNameSuffix " debug"
            resValue "string", "app_name", "Gatekeeper debug"
        }
        release {
            if (keyPropsRelease["store"] != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Gatekeeper"
        }
    }
}

dependencies {

    // Local
    compile project(':common')

    // Play Services
    compile 'com.google.android.gms:play-services-wearable:9.8.0'

    // Wear support libraries
    provided 'com.google.android.wearable:wearable:1.0.0'
    compile 'com.google.android.support:wearable:1.4.0'

    // Dagger 2
    compile "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided "javax.annotation:jsr250-api:1.0"

    // rx
    compile "io.reactivex:rxjava:${rxJavaVersion}"
    compile "io.reactivex:rxandroid:${rxAndroidVersion}"

    compile 'com.squareup.okio:okio:1.11.0'
    compile 'com.jakewharton.timber:timber:4.3.1'
}
apply from: '../config/static-analysis.gradle'
