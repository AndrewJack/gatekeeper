// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha1'
        classpath 'com.google.gms:google-services:3.0.0'
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }
}

subprojects {
    ext {
        gitSha = { ->
            try {
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            } catch (ignored) {
                return null;
            }
        }

        // keystore property
        def Properties localProps = new Properties()
        if (file('../local.properties').exists()) {
            localProps.load(new FileInputStream(file('../local.properties')))
        }

        keyPropsDebug = new Properties()
        if (localProps['keystore-debug.props.file'] != null) {
            keyPropsDebug.load(new FileInputStream(file(localProps['keystore-debug.props.file'])))
        }

        keyPropsRelease = new Properties()
        if (localProps['keystore-release.props.file'] != null) {
            keyPropsRelease.load(new FileInputStream(file(localProps['keystore-release.props.file'])))
        }

        appProps = new Properties()
        if (localProps['app.props.file'] != null) {
            appProps.load(new FileInputStream(file(localProps['app.props.file'])))
        }

        versionMajor = 1
        versionMinor = 2
        versionPatch = 0
        versionBuild = 0 // bump for dogfood builds, public betas, etc.

        targetSdk = 25
        compileSdk = 25
        minSdk = 23
        buildTools = "25.0.3"
        commonPlayServicesVersion = '10.2.6'
        commonSupportLibraryVersion = '25.3.1'
        daggerVersion = '2.11-rc2'
        rxJavaVersion = '2.1.0'
        rxAndroidVersion = '2.0.1'
    }

    project.configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support') {
                    details.useVersion commonSupportLibraryVersion
                }
            }
        }
    }
}

task clean(type: Delete) {
    group 'build'
    delete rootProject.buildDir
}
