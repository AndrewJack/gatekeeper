// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.1.3-2'
    ext.android_plugin_version = '3.0.0-alpha8'
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:3.1.0'
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }
}

subprojects {
    ext {
        gitSha = { ->
            try {
                def stdout = new ByteArrayOutputStream()
                exec {
                    commandLine 'git', 'rev-parse', '--short', 'HEAD'
                    standardOutput = stdout
                }
                return stdout.toString().trim()
            } catch (ignored) {
                return null
            }
        }

        // keystore property
        def Properties localProps = new Properties()
        if (file('../local.properties').exists()) {
            localProps.load(new FileInputStream(file('../local.properties')))
        }

        keyPropsDebug = new Properties()
        if (localProps['keystore-debug.props.file'] != null) {
            keyPropsDebug.load(new FileInputStream(file(localProps['keystore-debug.props.file'])))
        }

        keyPropsRelease = new Properties()
        if (localProps['keystore-release.props.file'] != null) {
            keyPropsRelease.load(new FileInputStream(file(localProps['keystore-release.props.file'])))
        }

        appProps = new Properties()
        if (localProps['app.props.file'] != null) {
            appProps.load(new FileInputStream(file(localProps['app.props.file'])))
        }

        versionMajor = 1
        versionMinor = 2
        versionPatch = 0
        versionBuild = 0 // bump for dogfood builds, public betas, etc.

        targetSdk = 26
        compileSdk = 26
        minSdk = 23
        buildTools = "26.0.1"
        commonPlayServicesVersion = '11.0.2'
        commonSupportLibraryVersion = '26.0.0'
        daggerVersion = '2.11'
        rxJavaVersion = '2.1.1'
        rxAndroidVersion = '2.0.1'
    }

    project.configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support') {
                    if (details.requested.name != 'multidex') {
                        details.useVersion commonSupportLibraryVersion
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    group 'build'
    delete rootProject.buildDir
}
